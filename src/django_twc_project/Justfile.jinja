set dotenv-load := true
set unstable := true

mod copier ".just/copier.just"
mod dj ".just/django.just"
mod docker ".just/docker.just"
mod docs ".just/documentation.just"
mod node ".just/node.just"
mod pg ".just/postgres.just"
mod project ".just/project.just"
mod prod ".just/prod.just"
mod py ".just/python.just"

export DATABASE_URL := env_var_or_default('DATABASE_URL', '{{ postgres_uri_scheme }}://postgres:postgres@db:5432/postgres')
export VIRTUAL_ENV := env_var_or_default('VIRTUAL_ENV', '.venv')
python_dir := if os_family() == "windows" { VIRTUAL_ENV + "/Scripts" } else { VIRTUAL_ENV + "/bin" }
export python := python_dir + if os_family() == "windows" { "/python.exe" } else { "/python" }
export system_python := if os_family() == "windows" { "py.exe" } else { "python" }
export uv := if which("uv") != "" { "uv" } else { "{% raw %}{{ python }}{% endraw %} -m uv" }

# List all available commands
[private]
default:
    @just --list

# Install and update all dependencies
bootstrap:
    @just docker build
    @just py install
    @just node install

# Build Docker images
build:
    @just docker build

# Clean up local development environment
clean:
    @just project clean
    @just docker clean

# Open a bash console within a specified container (default: 'app')
console CONTAINER="app":
    @just docker run {% raw %}{{ CONTAINER }}{% endraw %} "" "/bin/bash"

# Stop local development environment
down:
    @just docker down

# Run the linters on all files in project
lint:
    @just project lint

# Generate lockfiles for all dependencies
lock:
    @just py lock
    @just node lock

# Print out the logs of all Docker containers, optionally specifying a container to focus on
logs *ARGS:
    @just docker logs {% raw %}{{ ARGS }}{% endraw %}

# Run Django migrations
migrate *ARGS:
    @just dj migrate {% raw %}{{ ARGS }}{% endraw %}

# Alias for makemigrations
alias mm := makemigrations

# Generate Django migrations
makemigrations *ARGS:
    @just dj makemigrations {% raw %}{{ ARGS }}{% endraw %}

# Run a Django management command
manage *COMMAND:
    @just dj manage {% raw %}{{ COMMAND }}{% endraw %}

# Setup local development environment
setup:
    @just clean
    @just project setup
    @just bootstrap
    @just docker start
    @just dj migrate
    @just dj createsuperuser
    @just py test
    @just py cov-report
    @just py types
    @just project lint
    @just docker down

# Open a tool's interative shell within a specified container (default: 'app')
shell CONTAINER="app" COMMAND="":
    #!/usr/bin/env bash
    set -euo pipefail
    CONTAINER="{% raw %}{{ CONTAINER }}{% endraw %}"
    if [ $CONTAINER = "db" ]; then
        COMMAND="psql -d {% raw %}{{ DATABASE_URL }}{% endraw %}"
    elif [ $CONTAINER = "node" ] || [ $CONTAINER = "tailwind" ]; then
        COMMAND="node"
    elif [ $CONTAINER = "app" ] || [ $CONTAINER = "worker" ]; then
        COMMAND="ipython"
    elif [ $CONTAINER = "dj" ]; then
        CONTAINER="app"
        COMMAND="python -m manage shell_plus"
    fi
    [ -n "{% raw %}{{ COMMAND }}{% endraw %}" ] && COMMAND="{% raw %}{{ COMMAND }}{% endraw %}"
    COMMAND="${COMMAND:-/bin/bash}"
    just --quiet docker run $CONTAINER "" $COMMAND

# Start local development environment in background
start:
    @just docker start

# Stop local development environment
stop:
    @just docker stop

# Follow the logs of all Docker containers, optionally specifying a container to focus on
tail *ARGS:
    @just docker tail {% raw %}{{ ARGS }}{% endraw %}

# Run entire test suite, including generating code coverage
test *ARGS:
    @just py test {% raw %}{{ ARGS }}{% endraw %}
    @just py cov-report
    @just py cov-html

# Start local development environment in foreground
up:
    @just docker up

# Refresh and update local development environment
update:
    @just docker pull
    @just lock
    @just bootstrap

# Upgrade all dependencies to their newest versions
upgrade:
    @just py upgrade
    @just node upgrade
